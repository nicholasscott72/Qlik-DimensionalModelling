SUB MASTERTABLE(TYPE)
//LINKTABLE or CONCATENATED FACT TABLE SCRIPT
//RULES
//FACT TABLES PREFIXED "F_" and DIMENSION TABLES PREFIXED "D_"
//PRIMARY KEYS & FOREIGN KEYS PREFIXED "LINK_"
//PRIMARY KEY & FOREIGN KEY NAMES MUST BE IDENTICAL AS TABLE REFERED TO.

let vTables = NoOfTables();
for a = 0 to $(vTables)
	
	let vTable = TableName($(a));
	
	for i = 1 to NoOfFields('$(vTable)')
	  
	  let vField = FieldName($(i), '$(vTable)');
	    
	    t1:
	    load 
	    '$(vField)' as Field, 
	    '$(vTable)' as Table, 
	    $(a) as TableNo 
	    autogenerate 1
	    where left('$(vTable)',1) = 'F'
	    and left('$(vField)',4) = 'LINK';
	
	next i
	
next a

//CREATE FIELD LIST
t2:
	load 
	Table, 
	concat(Field,',') as FieldList
	resident t1
	Group by Table
	;

Drop Table t1;

//
t3:
	LOAD * INLINE [
    SourceTable
    StarterRecord    
];


IF TYPE = 'LINKTABLE' THEN
//CREATE LINK TABLE
	
For i = 1 to fieldvaluecount('Table')

	let vLoadTable = fieldvalue('Table',$(i));	
	let vPrimaryKey = replace(fieldvalue('Table',$(i)), 'F_','LINK_')&',';
	let vLoadFields = fieldvalue('FieldList',$(i));	
	let vDropFields = replace ( fieldvalue('FieldList',$(i)) , '$(vPrimaryKey)' , '');
	
		concatenate (t3) 
		load 
		$(vLoadFields),
		'$(vLoadTable)' as SourceTable
		resident $(vLoadTable);
	
	Drop Fields $(vDropFields) From $(vLoadTable);

Next i

ELSEif TYPE = 'FACT' THEN 


//SINGLE FACT TABLE
For i = 1 to fieldvaluecount('Table')

	let vLoadTable = fieldvalue('Table',$(i));	
	
		concatenate (t3) 
		load 
		*,
		'$(vLoadTable)' as SourceTable
		resident $(vLoadTable);
	
	Drop Tables $(vLoadTable);

Next i

END IF

MASTERTABLE:
NoConcatenate
LOAD
*
RESIDENT
t3
Where SourceTable <> 'StarterRecord'
;

Drop Table t2, t3;

END SUB
